import Qt5Compat.GraphicalEffects
import QtQuick
import QtQuick.Controls
import qs.common
import qs.common.functions
import qs.common.widgets

/**
 * A button with ripple effect similar to in Material Design.
 */
Button {
    id: root

    property bool toggled
    property string buttonText
    property bool pointingHandCursor: true
    property real buttonRadius: Rounding.small ?? 4
    property real topRadius:buttonRadius
    property real bottomRadius:buttonRadius
    property real buttonRadiusPressed: buttonRadius
    property real buttonEffectiveRadius: root.down ? root.buttonRadiusPressed : root.buttonRadius
    property int rippleDuration: 1200
    property bool rippleEnabled: true
    property var downAction // When left clicking (down)
    property var releaseAction // When left clicking (release)
    property var altAction // When right clicking
    property var middleClickAction // When middle clicking
    property color colBackground: colors.colLayer1 || "transparent"
    property color colBackgroundHover: colors.colLayer1Hover ?? "#E5DFED"
    property color colBackgroundToggled: colors.colPrimary ?? "#65558F"
    property color colBackgroundToggledHover: colors.colPrimaryHover ?? "#77699C"
    property color colRipple: colors.colLayer1Active ?? "#D6CEE2"
    property color colRippleToggled: colors.colPrimaryActive ?? "#D6CEE2"
    property color buttonColor: ColorUtils.transparentize(root.toggled ? (root.hovered ? colBackgroundToggledHover : colBackgroundToggled) : (root.hovered ? colBackgroundHover : colBackground), root.enabled ? 0 : 1)
    property color rippleColor: root.toggled ? colRippleToggled : colRipple
    property QtObject colors: Colors

    function startRipple(x, y) {
        const stateY = buttonBackground.y;
        rippleAnim.x = x;
        rippleAnim.y = y - stateY;
        const dist = (ox, oy) => {
            return ox * ox + oy * oy;
        };
        const stateEndY = stateY + buttonBackground.height;
        rippleAnim.radius = Math.sqrt(Math.max(dist(0, stateY), dist(0, stateEndY), dist(width, stateY), dist(width, stateEndY)));
        rippleFadeAnim.complete();
        rippleAnim.restart();
    }

    opacity: root.enabled ? 1 : 0.4

    MouseArea {
        anchors.fill: parent
        cursorShape: root.pointingHandCursor ? Qt.PointingHandCursor : Qt.ArrowCursor
        acceptedButtons: Qt.LeftButton | Qt.RightButton | Qt.MiddleButton
        onPressed: event => {
            if (event.button === Qt.RightButton) {
                if (root.altAction)
                    root.altAction(event);

                return;
            }
            if (event.button === Qt.MiddleButton) {
                if (root.middleClickAction)
                    root.middleClickAction();

                return;
            }
            root.down = true;
            if (root.downAction)
                root.downAction();

            if (!root.rippleEnabled)
                return;

            const {
                "x": x,
                "y": y
            } = event;
            NoonUtils.playSound("event_accepted");
            startRipple(x, y);
        }
        onReleased: event => {
            root.down = false;
            if (event.button != Qt.LeftButton)
                return;

            if (root.releaseAction)
                root.releaseAction();

            root.click(); // Because the MouseArea already consumed the event
            if (!root.rippleEnabled)
                return;

            rippleFadeAnim.restart();
        }
        onCanceled: event => {
            root.down = false;
            if (!root.rippleEnabled)
                return;

            rippleFadeAnim.restart();
        }
    }

    RippleAnim {
        id: rippleFadeAnim

        duration: rippleDuration * 2
        target: ripple
        property: "opacity"
        to: 0
    }

    SequentialAnimation {
        id: rippleAnim

        property real x
        property real y
        property real radius

        PropertyAction {
            target: ripple
            property: "x"
            value: rippleAnim.x
        }

        PropertyAction {
            target: ripple
            property: "y"
            value: rippleAnim.y
        }

        PropertyAction {
            target: ripple
            property: "opacity"
            value: 1
        }

        ParallelAnimation {
            RippleAnim {
                target: ripple
                properties: "implicitWidth,implicitHeight"
                from: 0
                to: rippleAnim.radius * 2
            }
        }
    }

    component RippleAnim: Anim {
        duration: rippleDuration
        easing.type: Easing.BezierSpline
        easing.bezierCurve: Animations.curves.standardDecel
    }

    background: StyledRect {
        id: buttonBackground
        topRadius:root.buttonEffectiveRadius
        bottomRadius:root.buttonEffectiveRadius
        // radius: root.buttonEffectiveRadius
        implicitHeight: 30
        color: root.buttonColor
        layer.enabled: true

        Item {
            id: ripple

            property real implicitWidth: 0
            property real implicitHeight: 0

            width: ripple.implicitWidth
            height: ripple.implicitHeight
            opacity: 0
            visible: width > 0 && height > 0

            RadialGradient {
                anchors.fill: parent

                gradient: Gradient {
                    GradientStop {
                        position: 0
                        color: root.rippleColor
                    }

                    GradientStop {
                        position: 0.3
                        color: root.rippleColor
                    }

                    GradientStop {
                        position: 0.5
                        color: Qt.rgba(root.rippleColor.r, root.rippleColor.g, root.rippleColor.b, 0)
                    }
                }
            }

            Behavior on opacity {
                Anim {}
            }

            transform: Translate {
                x: -ripple.width / 2
                y: -ripple.height / 2
            }
        }

        Behavior on color {
            CAnim {}
        }

        layer.effect: OpacityMask {

            maskSource: Rectangle {
                width: buttonBackground.width
                height: buttonBackground.height
                radius: root.buttonEffectiveRadius
            }
        }
    }

    contentItem: StyledText {
        text: root.buttonText
    }
}
